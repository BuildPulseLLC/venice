
task buildJar(type: Jar) {
  manifest {
    attributes 'Implementation-Title': 'Venice Common',
        'Implementation-Version': version
  }
  baseName = project.name
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

jar {
  manifest {
    attributes 'Implementation-Title': 'Venice Common',
        'Implementation-Version': version
  }
}

sourceSets {
  // Tests work in IntelliJ regardless of whether this config is present or not,
  // but it is necessary in order for Gradle's tests to find the main resources.
  test {
    // At the time of this writing, the only relevant resources under main which
    // the code needs are the Avro schema definitions under: src/main/resources/avro/
    output.resourcesDir = "build/resources/main"
  }
}


dependencies {

  // Let's try to keep it clean! (i.e.: alphabetical)

  compile (libraries.avro) {
    exclude group: 'org.jboss.netty' // older version of netty3 causes transitive conflicts with the router
    exclude group: 'org.mortbay.jetty' // jetty 6 conflicts with spark-java used in controller api
    exclude group: 'org.slf4j'        // Avro pulls in slf4j 1.5 which is API incompatible with 1.6
  }
  compile libraries.commonsCodec
  compile libraries.guava
  compile (libraries.helix) { // TODO: Get rid of Helix dependency in venice-common
    // Helix pulls in an ancient version of the zkclient, which lacks some APIs and clashes with Kafka's dependency
    exclude group: 'com.github.sgroschupf'
    // replacing Helix's netty 3.7 with 3.9 (netty3 below) so the router doesn't get 3.7 when it depends on common
    exclude group: 'io.netty'
  }
  compile libraries.netty3
  compile libraries.asyncHttpClient
  compile libraries.kafkaClients // TODO: Get rid of Kafka dependency in venice-common
  compile libraries.zkclient // It's necessary to pull in the most recent version of zkclient explicitly, otherwise Helix won't have it...
  compile libraries.zookeeper // TODO: Get rid of ZK dependency in venice-common
  compile libraries.jacksonMapper

  compile libraries.kafka // TODO: Get rid of Kafka dependency in venice-common (used by TopicCreator)

  // Test dependencies are allowed to be bulkier.
  // testCompile libraries.kafka //restore this if we remove it from the compile dependencies
  testCompile libraries.zookeeper
  testCompile project(':venice-controller')
  testCompile project(':venice-server')
  testCompile project(':venice-router')
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

test {
  useTestNG()
}
