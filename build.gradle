apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

ext.libraries = [ // Groovy map literal
                  avro: 'org.apache.avro:avro:1.4.1',
                  bdbJe: 'com.sleepycat:je:5.0.104',
                  commonsCodec: 'commons-codec:commons-codec:1.4',
                  guava: 'com.google.guava:guava:18.0',
                  helix: 'org.apache.helix:helix-core:0.6.5',
                  kafka: 'kafka:kafka_2.10:0.9.0.2', // Internal LinkedIn release
                  kafkaClients: 'kafka:kafka-clients:0.9.0.2', // Internal LinkedIn release
                  netty: 'io.netty:netty-all:4.0.33.Final',
                  zkclient: 'com.101tec:zkclient:0.7', //for Kafka AdminUtils
                  router: 'com.linkedin.dds-storage-core:dds-storage-router-api:0.0.34',

                  // Hadoop to Venice Bridge dependencies
                  avroTools: 'org.apache.avro:avro-tools:1.4.0', // N.B.: 1.4.1 does not exist, hence the discrepancy
                  commonsConfiguration: 'commons-configuration:commons-configuration:1.9',
                  commonsLang: 'commons-lang:commons-lang:2.6',
                  hadoopCommon: 'org.apache.hadoop:hadoop-common:2.3.0',
                  httpClient: 'org.apache.httpcomponents:httpclient:4.2.1',
                  httpCore: 'org.apache.httpcomponents:httpcore:4.2.1',
                  jacksonCore: 'org.codehaus.jackson:jackson-core-asl:1.9.11',
                  jacksonMapper: 'org.codehaus.jackson:jackson-mapper-asl:1.9.11',
                  jdom: 'org.jdom:jdom:1.1',
                  joptSimple: 'net.sf.jopt-simple:jopt-simple:3.2',
                  log4j: 'log4j:log4j:1.2.17',
                  metricsCore: 'com.yammer.metrics:metrics-core:2.2.1',
                  snappy: 'org.xerial.snappy:snappy-java:1.0.4.1',
                  testng: 'org.testng:testng:6.1.1'
]

subprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'eclipse'

  sourceCompatibility = 1.8
  version = '0.1'

  repositories {
    mavenCentral()
    maven {
      url "http://download.oracle.com/maven/"
    }
    ivy {  //TODO: remove this before opensourcing
      url 'http://artifactory.corp.linkedin.com:8081/artifactory/release/'
      layout "pattern", {
        ivy '[organisation]/[module]/[revision]/[module]-[revision].ivy'
        artifact '[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier]).[ext]'
        m2compatible = true
      }
    }
  }

  dependencies {
    compile 'org.slf4j:slf4j-log4j12:1.7.14'
    compile 'javax.validation:validation-api:1.0.0.GA'
    testCompile 'org.testng:testng:6.1.1'

    //PowerMock for mocking private methods, class members, enums, etc.
    testCompile 'org.powermock:powermock-mockito-release-full:1.5.6'
    testCompile 'org.mockito:mockito-all:1.9.5'
  }

  idea {
    module {
      downloadJavadoc = true
      downloadSources = true
    }
  }
}

task buildAll {
  dependsOn 'venice-common:build'
  dependsOn 'venice-client:build'
  dependsOn 'venice-server:build'
  dependsOn 'venice-controller:build'
  dependsOn 'hadoop-to-venice-bridge:build'

  dependsOn 'venice-common:buildJar'
  dependsOn 'venice-client:installApp'
  dependsOn 'venice-server:installApp'
  dependsOn 'venice-controller:installApp'
  // dependsOn 'hadoop-to-venice-bridge:installApp'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
